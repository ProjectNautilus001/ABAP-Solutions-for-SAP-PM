*&---------------------------------------------------------------------*
*& Report Y_PM_MEL_LTXT_EXP.
*& Kann im Hintergrund (JOB) ausgeführt werden.
*&---------------------------------------------------------------------*
*& Langtexte aus Störungsmeldungen (QMEL) für definierten Zeitraum.
*& Export als *.csv. !Dateipfad muss in SAP definiert sein!
*&
*& Author     : 
*& date       : [07.06.2025]
*& kind of p. : Report    (x)     Batch-Input ( )      Include   ( )
*&              Sonstiges/others  ( )                  Modulpool   ( )
*----------------------------------------------------------------------*
* Änderungshistorie                                                    *
* Datum      Benutzer:                  Grund                          *
* ---------- ----------------- --------------------------------------- *
*&---------------------------------------------------------------------*
REPORT Y_PM_MEL_LTXT_EXP.

DATA: lt_notifications TYPE TABLE OF qmel,
      ls_notification  TYPE qmel,
      lt_text          TYPE TABLE OF tline,
      ls_text          TYPE tline,
      lv_name          TYPE tdobname,
      lt_output        TYPE TABLE OF string,
      lv_output        TYPE string,
      lv_file          TYPE string,
      lv_date_line     TYPE string.

PARAMETERS: p_datefr TYPE sy-datum OBLIGATORY,
            p_dateto TYPE sy-datum OBLIGATORY,
            p_csv    TYPE c AS CHECKBOX,
            p_file   TYPE string LOWER CASE.

SELECT-OPTIONS: s_qmart FOR ls_notification-qmart OBLIGATORY DEFAULT 'M2'.

INITIALIZATION.
  s_qmart-sign = 'I'.
  s_qmart-option = 'EQ'.
  s_qmart-low = 'M2'.
  APPEND s_qmart.

START-OF-SELECTION.
  SELECT * INTO TABLE lt_notifications
    FROM qmel
    WHERE qmart IN s_qmart
      AND erdat BETWEEN p_datefr AND p_dateto.

  IF lt_notifications IS INITIAL.
    MESSAGE 'Keine Meldungen gefunden' TYPE 'I' DISPLAY LIKE 'E'.
    RETURN.
  ENDIF.

  LOOP AT lt_notifications INTO ls_notification.
    lv_name = ls_notification-qmnum.

    CALL FUNCTION 'READ_TEXT'
      EXPORTING
        id       = 'LTXT'
        language = sy-langu
        name     = lv_name
        object   = 'QMEL'
      TABLES
        lines    = lt_text
      EXCEPTIONS
        OTHERS   = 1.

    lv_output = |Meldungsnummer;{ ls_notification-qmnum };Priorität;{ ls_notification-priok }|.
    APPEND lv_output TO lt_output.

    IF sy-subrc = 0 AND lt_text IS NOT INITIAL.
      LOOP AT lt_text INTO ls_text.
        lv_output = ls_text-tdline.
        CONDENSE lv_output.
        APPEND lv_output TO lt_output.
      ENDLOOP.
    ELSE.
      APPEND ';Kein Langtext vorhanden;' TO lt_output.
    ENDIF.

    APPEND ' ' TO lt_output.

    CLEAR: lt_text, ls_text.
  ENDLOOP.

  IF p_csv = 'X' AND lt_output IS NOT INITIAL.
    IF p_file IS INITIAL.
      MESSAGE 'Bitte Dateipfad angeben' TYPE 'E'.
      RETURN.
    ENDIF.

    lv_file = p_file.
    IF lv_file NP '*.csv'.
      CONCATENATE lv_file '.csv' INTO lv_file.
    ENDIF.

    OPEN DATASET lv_file FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
    IF sy-subrc = 0.

      " Erstellungsdatum als erste Zeile
      lv_date_line = |Stand: { sy-datum+6(2) }.{ sy-datum+4(2) }.{ sy-datum(4) }|.
      TRANSFER lv_date_line TO lv_file.

      LOOP AT lt_output INTO lv_output.
        TRANSFER lv_output TO lv_file.
      ENDLOOP.

      CLOSE DATASET lv_file.
      MESSAGE |Datei { lv_file } erfolgreich erstellt| TYPE 'S'.
    ELSE.
      MESSAGE |Fehler beim Öffnen der Datei { lv_file }| TYPE 'E'.
    ENDIF.
  ELSEIF lt_output IS INITIAL.
    MESSAGE 'Keine Texte gefunden' TYPE 'I' DISPLAY LIKE 'W'.
  ENDIF.
